pkgbase=faudio-git
pkgname=(faudio-git lib32-faudio-git)
pkgver=19.05.r29.gc724fb2
pkgrel=1

pkgdesc="XAudio2 reimplementation"
arch=(x86_64)
url="https://github.com/FNA-XNA/FAudio/"
license=('custom:zlib')

makedepends=('git' 'cmake')
source=('git+https://github.com/FNA-XNA/FAudio'
        'faudio.pc'
        'faudio-32.pc'
        'force-lib32-sdl2.patch')

sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd FAudio
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build build-32
  cp -r FAudio FAudio-32
  cd FAudio-32
  patch -p1 -i ../force-lib32-sdl2.patch
}

build() {
  cd "$srcdir/build"
  cmake ../FAudio \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DFFMPEG=ON
  make


  cd "$srcdir/build-32"

  export CC="gcc -m32 -mstackrealign"
  export CXX="g++ -m32 -mstackrealign"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake ../FAudio-32 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -DFFMPEG=ON \
    -DFFmpeg_LIBRARY_DIRS=/usr/lib32
  make
}

package_faudio-git() {
  provides=('faudio')
  depends=('sdl2' 'ffmpeg')
  DESTDIR="$pkgdir" make -C build install

  install -Dm644 FAudio/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm644 faudio.pc "$pkgdir"/usr/lib/pkgconfig/faudio.pc
}

package_lib32-faudio-git() {
  provides=('lib32-faudio')
  depends=('faudio-git' 'lib32-sdl2' 'lib32-ffmpeg')
  DESTDIR="$pkgdir" make -C build-32 install
  rm -r "$pkgdir"/usr/include

  install -Dm644 FAudio/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm644 faudio-32.pc "$pkgdir"/usr/lib32/pkgconfig/faudio.pc
}
